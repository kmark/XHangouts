def gitRev = {
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return 'nogit';
    }
}

Closure<String[]> getVersionDef = {
    new File('app/versionDef.txt').text.trim().split('\\r?\\n')
}

Closure<String> getVersionName = {
    getVersionDef()[1] + '-' + gitRev()
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.0"

    defaultConfig {
        applicationId "com.versobit.kmark.xhangouts"
        minSdkVersion 19
        targetSdkVersion 24
        versionCode getVersionDef()[0].toInteger()
        versionName getVersionName()
    }

    aaptOptions {
        cruncherEnabled = false
    }

    lintOptions {
        disable 'MissingTranslation'
    }
}

dependencies {
    provided 'de.robv.android.xposed:api:53'
    compile 'eu.chainfire:libsuperuser:1.0.0.201608240809'
    compile project(':afilechooser')
    compile project(':colorpicker')
}

// Find and apply the signing file
// See signing.template.gradle for more information
if (project.hasProperty('signing.file')) {
    if (new File(project.property('signing.file') as String).exists()) {
        apply from: project.property('signing.file')
    } else {
        throw new InvalidUserDataException('Signing configuration file does not exist: ' +
                project.property('signing.file'))
    }
} else {
    logger.log(LogLevel.WARN, 'WARNING: No signing configuration file defined.')
}
